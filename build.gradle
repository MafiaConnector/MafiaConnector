plugins {
    id 'java'
    id 'application'
    id 'com.google.protobuf' version '0.9.4'
}

group = 'com.ksk'
version = '1.0-SNAPSHOT'

// Set the main class for the application plugin
mainClassName = 'com.ksk.mf.gui.MafiaConnectorApp'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        proto {
            srcDir 'src/main/proto'
        }
    }
}

tasks.withType(Copy).configureEach {
    filesMatching("**/*.proto") {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
    implementation 'io.netty:netty-all:4.1.99.Final'
    implementation 'org.fusesource.jansi:jansi:1.11'
    implementation("ch.qos.logback:logback-classic:1.3.11")
    implementation group: 'com.google.protobuf', name: 'protobuf-kotlin-lite', version: '3.25.2'
    implementation 'org.reflections:reflections:0.10.2'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.24.4'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
                kotlin {
                    option 'lite'
                }
            }
        }
    }
}

test {
    useJUnitPlatform()
}

// Function to load .env file
def loadEnvFile() {
    def envVars = [:]
    def envFile = file('.env')
    if (envFile.exists()) {
        envFile.readLines().each { line ->
            def trimmed = line.trim()
            if (!trimmed.isEmpty() && !trimmed.startsWith('#')) {
                def parts = trimmed.split('=', 2)
                if (parts.length == 2) {
                    def key = parts[0].trim()
                    def value = parts[1].trim()
                    envVars[key] = value
                }
            }
        }
    }
    return envVars
}

// Task to run the JavaFX GUI application
tasks.register('runGui', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.ksk.mf.gui.MafiaConnectorApp'

    // Set file encoding to UTF-8
    systemProperty 'file.encoding', 'UTF-8'

    // Load .env file and set environment variables
    environment loadEnvFile()
}

// Task to run the console application
tasks.register('runConsole', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.ksk.mf.Main'

    // Load .env file and set environment variables
    environment loadEnvFile()
}

// Configure default run task to also load .env
tasks.named('run', JavaExec) {

    environment loadEnvFile()
}

